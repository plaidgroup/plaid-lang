/*
 * Copyright (c) 2010 The Plaid Group (see AUTHORS file)
 * 
 * This file is part of Plaid Programming Language.
 *
 * Plaid Programming Language is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 *  Plaid Programming Language is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with Plaid Programming Language.  If not, see <http://www.gnu.org/licenses/>.
 */

package coreExamples.stateVarsTest;

state TOP { }

state S case of TOP {

	val a;
	
	val lambda = fn (a) => { a };
	
	method b();
	
	//Test method bodies (singleton)
	method b1() { this.a }
	
	//Test applications and method calls
	method c() {
		print(this.a);
		print(b());
		print(b1());
	}
	
	//Test inside state init decls
	method i() {
		new S { a = a; };
	}
	
	//Test shadowing in lambdas
	method j() { lambda (3) }

	//Test shadowing in sequence expressions/local variables
	method k() {
		var a = 1;
		a
	}

}

method testS() {
	print("should be 555: ");
	var test = new S { a = 5; method b() { 5 } };
	test.c();
}

state T case of S {

	val d = new S { a = 4; };

	//Test dereferences
	method e() {
		this.d.a
	}
	
	//Test that it goes inside match statements
	method f() {
		match (this) {
			case T { this.d.a }
			//default { print("should be 4444: "); }
		}
	}
	
	//Test shadowing of method parameters
	method g(d) {
		match (d) {
			case T { d.d.a }
			case S { d.a }
			default { "matching seems to have failed" }
		}
	}
	
	//Test inside state change decls
	method h() {
		this <- S { a = this.d.a; };
	}
}

method testT() {
	print("should be 4444: ");
	var test = new T;
	print(test.e());
	print(test.f());
	print(test.g(test));
	print(test.g(test.d));
	
	printLine("");
	print("should be 4431: ");
	var test2 = new T {a = 4; };
	test2.h(); 
	print(test2.a);
	print(test2.i().a);
	print(test2.j());
	print(test2.k());
	
}

method main() {
	testS();
	printLine("");
	testT();
	printLine("");

}