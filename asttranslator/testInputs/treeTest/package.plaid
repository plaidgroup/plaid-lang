package plaid.typechecker.tests.treeTest;

import plaid.lang.*;

// Top-level tree node.
state Node {}

// Leaf node.
state Leaf case of Node {
	method insert(elem) {
		this <- Branch with {
			var data = elem;
			var left = new Leaf;
			var right = new Leaf;
		};
	}

	method contains(elem) {
		false;
	}

	method remove(elem) {
		false;
	}

	method isLeaf() {
		true;
	}

	method depth() {
		0;
	}

	method size() {
		0;
	}

	method print(depth) {
		//java.lang.System.out.println("Leaf at " + depth);
		//Never mind, just do nothing.
	}
}

// Branch node.
state Branch case of Node {
	var data;
	var left;
	var right;

	method insert(elem) {
		ifElse (elem > data) {
			right.insert(elem);
		}{
			ifElse (elem < data) {
				left.insert(elem);
			}{
				false;
			};
		};
	}

	method contains(elem) {
		ifElse (elem == data) {
			true;
		}{
			ifElse (elem > data) {
				right.contains(elem);
			}{
				left.contains(elem);
			};
		};
	}

	method remove(elem) {
		ifElse (elem == data) {
			removeSelf();
			true;
		}{
			ifElse (elem > data) {
				right.remove(elem);
			}{
				left.remove(elem);
			};
		};
	}

	method removeSelf() {
		ifElse(left.isLeaf()) {
			ifElse(right.isLeaf()) {
				this <- Leaf;
			}{
				data = right.data;
				left = right.left;
				right = right.right;
			};
		}{
			ifElse(right.isLeaf()) {
				data = left.data;
				right = left.right;
				left = left.left;
			}{
				ifElse(left.depth() > right.depth()) {
					data = left.removeRightmost();
				}{
					data = right.removeLeftmost();
				};
			};
		};
	}

	method isLeaf() {
		false;
	}

	method depth() {
		val depth_l = left.depth();
		val depth_r = right.depth();
		ifElse(depth_l > depth_r) {
			depth_l;
		}{
			depth_r;
		};
	}

	method size() {
		1 + left.size() + right.size();
	}

	method print(depth) {
		left.print(depth+1);
		java.lang.System.out.println("" + data + " at " + depth);
		right.print(depth+1);
	}

	method removeLeftmost() {
		ifElse(left.isLeaf()) {
			val data2 = data;
			removeSelf();
			data2;
		}{
			left.removeLeftmost();
		};
	}

	method removeRightmost() {
		ifElse(right.isLeaf()) {
			val data2 = data;
			removeSelf();
			data2;
		}{
			right.removeRightmost();
		};
	}
}

// The tree itself.
state Tree {
	val root = new Leaf;

	method insert(elem) {
		root.insert(elem);
	}

	method contains(elem) {
		root.contains(elem);
	}

	method remove(elem) {
		root.remove(elem);
	}

	method print() {
		root.print(0);
	}
}

method main() {
	val tree = new Tree;

	tree.insert(40);
	tree.insert(20);
	tree.insert(60);
	tree.insert(10);
	tree.insert(30);
	tree.insert(50);
	tree.insert(70);

	if (tree.contains(30)) {
		java.lang.System.out.println("Tree contains 30.");
	};

	tree.print();
	java.lang.System.out.println();

	tree.remove(40);
	tree.print();
}