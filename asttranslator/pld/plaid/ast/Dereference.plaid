package plaid.ast;

state Dereference case of Expression {
	val /*Token*/ token;
	val /*Expression*/ left;
	val /*ID*/ right;

	method accept(v) {
		v.visitDereference(this)
	}
	
	method copy() {
		new Dereference {
			val /*Token*/ token = this.token;
			val /*Expression*/ left = this.left.copy();
			val /*ID*/ right = this.right.copy();
		}
	}	
	
	method immutable String toString()[immutable Dereference] {
		"DEREF(" + left.toString() + "." + right.name + ")"
	}
	
	method immutable Boolean ==(immutable ASTNode other)[immutable Dereference] {
		match (other) {
			case Dereference {
				this.left == other.left && this.right == other.right && this.objectType == other.objectType
			}
			default { false }
		}
	}
}