package plaid.ast;

state Match case of Expression {
	val /*Token*/ token;
	val /*Expression*/ e;
	val /*List<Case>*/ caseList;

	method accept(v) {
		v.visitMatch(this)
	}
	
	method copy() {
		new Match {
			val /*Token*/ token = this.token;
			val /*Expression*/ e = this.e.copy();
			val /*List<Case>*/ caseList = this.caseList.map(fn (c) => c.copy());
		}
	}
	
	method immutable String toString()[immutable Match] {
		val caseString = this.caseList.toString();
		"MATCH(" + this.e.toString() + "{ " + caseString + " })";
	}
	
	method immutable Boolean ==(immutable ASTNode other)[immutable Match] {
		match (other) {
			case Match {
				this.caseList == other.caseList && this.e == other.e && this.objectType == other.objectType
			}
			default { false }
		}
	}
}