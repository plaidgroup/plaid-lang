package plaid.typechecker.AST.visitor;

state PrintVisitor case of ASTVisitor {
	var indent = 0;
	
	method output(s) {
		var i = 0;
		while { i < indent; } {
			java.lang.System.out.print("  ");
			i = i + 1
		};
		java.lang.System.out.println(s);
	}
	
	method inc() {
		indent = indent + 1
	}
	
	method dec() {
		indent = indent - 1
	}
	
	method visitApplication(node) {
		output("Application");
		inc();
		node.f.accept(this);
		node.arg.accept(this);
		dec();
	}
	
	method visitAssignment(node) {
		output("Assignment");
		inc();
		if (!node.target.isNull()) {
			node.target.accept(this);
		};
		node.field.accept(this);
		node.value.accept(this);
		dec();		
	}
	
	method visitCase(node) {
		output("Case");
		inc();
		node.qi.accept(this);
		node.x.accept(this);
		node.e.accept(this);
		dec();
	}
	
	method visitChangeState(node) {
		output("ChangeState");
		inc();
		node.e.accept(this);
		node.s.accept(this);
		dec();
	}
	
	method visitCompilationUnit(node) {
		output("CompilationUnit");
		inc();
		node.decls.map(fn (decl) => {
			decl.accept(this)
		});
		node.imports.accept(this);
		dec();
	}
	
	method visitDeclList(node) {
		output("DeclList");
		inc();
		node.decls.map(fn (decl) => {
			decl.accept(this)
		});
		dec();
	}
	
	method visitDereference(node) {
		output("Dereference");
		inc();
		node.left.accept(this);
		node.right.accept(this);
		dec();
	}

	method visitDestructiveDereference(node) {
		output("DestructiveDereference");
		inc();
		node.left.accept(this);
		node.right.accept(this);
		dec();
	}
	
	method visitFieldDecl(node) {
		output("FieldDecl");
		inc();
		node.f.accept(this);
		node.e.accept(this);
		dec();
	}
	
	method visitID(node) {
		output("ID(" + node.name + ")");
	}
	
	method visitImportList(node) {
		output("ImportList");
	}
	
	method visitIntLiteral(node) {
		output("IntLiteral(" + node.integer + ")");
	}
	
	method visitLambda(node) {
		output("Lambda");
		inc();
		node.x.accept(this);
		node.body.accept(this);
		dec();
	}
	
	method visitLetBinding(node) {
		output("LetBinding");
		inc();
		node.x.accept(this);
		node.exp.accept(this);
		node.body.accept(this);
		dec();
	}
	
	method visitMatch(node) {
		output("Match");
		inc();
		node.e.accept(this);
		node.caseList.map(fn (c) => {
			c.accept(this)
		});
		dec();
	}
	
	method visitMethodDecl(node) {
		output("MethodDecl(" + node.name + ")");
		inc();
		node.body.accept(this);
		if (!node.arg.isNull()) {
			node.arg.accept(this);
		};
		dec();
	}
	
	method visitMethodInvocation(node) {
		output("MethodInvocation");
		inc();
		node.obj.accept(this);
		node.function.accept(this);
		node.arg.accept(this);
		dec();
	}
	
	method visitNewInstance(node) {
		output("NewInstance");
//		java.lang.System.out.println(node);
		inc();
		node.st.accept(this);
		dec();
	}
	
	method visitNullNode(node) {
		output("NULL");
	}
	
	method visitQI(node) {
		output("QI");
	}
	
	method visitStateDecl(node) {
		output("StateDecl");
		inc();
		node.name.accept(this);
		node.stateDef.accept(this);
		if (!node.caseOf.isNull()) {
			node.caseOf.accept(this);
		};
		dec();
	}
	
	method visitStringLiteral(node) {
		output("StringLiteral('" + node.string + "')");
	}
	
	method visitThrows(node) {
		output("Throws");
		inc();
		node.throwableID.accept(this);
		dec();
	}
	
	method visitUnitLiteral(node) {
		output("UnitLiteral");
	}
	
	method visitWith(node) {
		output("With");
		inc();
		node.s1.accept(this);
		node.s2.accept(this);
		dec();
	}
}