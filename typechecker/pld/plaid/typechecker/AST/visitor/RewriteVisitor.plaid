package plaid.typechecker.AST.visitor;

import plaid.typechecker.AST.*;

state RewriteVisitor {
	var localVars = new MyList with { var head = new MyNil; };
	var stateVars = new MySet with { var head = new MySetNil; };
	var imports = 0;
	var packageName = 0;

	method contains(vars, node) {
		var found = false;
		
		vars.map(fn (v) => {
			if (v == node) {
				found = true;
			};
		});
		
		found;
	}

	method visitAssignment(node) {
		if (node.target.isNull()) {
			if (!contains(localVars, node.field) && contains(stateVars, node.field)) {
				node.target = new ID { val name = "this$plaid"; };
			}
		};
		node.field.accept(this);
		node.value.accept(this);
	}

	method visitCase(node) {
		if (!node.defaultCase && node.boundVar) {
			localVars.append(node.x);
		};
		
		node.qi.accept(this);
		node.x.accept(this);
		node.e.accept(this);
		
		if (!node.defaultCase && node.boundVar) {
			localVars.remove(node.x);
		};
	}
	
	method visitCompilationUnit(node) {
		imports = node.javaImports;
		packageName = node.packageName;
		
		node.decls.map(fn (decl) => {
			localVars.append(new ID { val name = decl.getName(); })
		});
		
		node.decls.map(fn (decl) => {
			decl.accept(this)
		});
		
		node.decls.map(fn (decl) => {
			localVars.remove(new ID { val name = decl.getName(); })
		});
	}

	method visitID(node) {
		if (!contains(localVars, node) && contains(stateVars, node)) {
			node <- Dereference {
				override val token = node.token;
				val left = new ID { val name = "this$plaid"; };
				val right = new ID { val name = node.name; };
			};
			// node.accept(this);
		}
	}

	method visitLambda(node) {
		localVars.append(node.x);

		node.x.accept(this);
		node.body.accept(this);
		
		localVars.remove(node.x);
	}

	method visitMatch(node) {
		node.e.accept(this);
		node.caseList.map(fn (c) => {
			c.accept(this);
		});
	}
	
	method visitMethodDecl(node) {
		if (!node.name.equals("main")) {
			localVars.append(node.arg);
		};
		
		node.body.accept(this);
		node.arg.accept(this);
		
		if (!node.name.equals("main")) {
			localVars.remove(node.arg);
		};
	}

	method generateStateVars(node) {
		var helper = plaid.compilerjava.AST.StateDeclHelper.new();
		var qid = plaid.compilerjava.util.QualifiedID.new(packageName);
		// TODO: Handle caseOf!
		var l = helper.generateStateVars(qid, imports.getImports(), node.javaStateDef);
		var mySet = new MySet { var head = new MySetNil; };
		val iter = l.iterator();
		java.lang.System.out.println(l);
		
		while { iter.hasNext(); } {
			val javaID = iter.next();
			mySet.add(new ID { val name = javaID.getName(); });
		};
		
		mySet;
	}

	method visitStateDecl(node) {
		stateVars = generateStateVars(node);
		
		node.name.accept(this);
		node.stateDef.accept(this);
		node.caseOf.accept(this);
		
		stateVars = new MySet { var head = new MySetNil; };
	}
	
	method visitApplication(node) {
		node.f.accept(this);
		node.arg.accept(this)
	}
	
	method visitChangeState(node) {
		node.e.accept(this);
		node.s.accept(this)
	}
	
	method visitDeclList(node) {
		node.decls.map(fn (decl) => {
			decl.accept(this)
		});
	}
	
	method visitDereference(node) {
		node.left.accept(this);
		// The right child is always an ID and we don't need to visit it
		// node.right.accept(this);
	}
	
	method visitFieldDecl(node) {
		// Does not need to be visited
		// node.f.accept(this);
		node.e.accept(this)
	}
	
	method visitImportList(node) {
		node.imports.map(fn (im) => {
			im.accept(this)
		});
	}
	
	method visitIntLiteral(node) {
	}
	
	method visitInvalidNode(node) {
	}
	
	method visitLetBinding(node) {
		node.x.accept(this);
		node.exp.accept(this);
		node.body.accept(this)
	}
	
	method visitNewInstance(node) {
		node.st.accept(this)
	}
	
	method visitNullNode(node) {
	}
	
	method visitQI(node) {
	}
	
	method visitStringLiteral(node) {
	}
	
	method visitUnitLiteral(node) {
	}
	
	method visitWith(node) {
		node.s1.accept(this);
		node.s2.accept(this)
	}	
}