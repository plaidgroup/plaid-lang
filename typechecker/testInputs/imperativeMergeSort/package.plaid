/**
 * Copyright (c) 2010 The Plaid Group (see AUTHORS file)
 * 
 * This file is part of Plaid Programming Language.
 *
 * Plaid Programming Language is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 *  Plaid Programming Language is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with Plaid Programming Language.  If not, see <http://www.gnu.org/licenses/>.
 */

package testInputs.imperativeMergeSort;

state List {
	method void add(immutable Integer >> void i)[unique List >> unique List];
	method unique List splitList()[unique List >> unique List];
}

state Cons case of List {
	var unique List next;
	val immutable Integer data;
	
	method void add(immutable Integer >> void i)[unique Cons >> unique Cons] {
		val unique List addTo = this!next; //unpack this
		addTo.add(i);
		this.next = addTo; //repack this
	}
	
	method unique List splitList()[unique Cons >> unique Cons] {
		match (this!next) {
			case Cons {
				val unique List odd = this!next; //unpack this
				this.next = odd.splitList(); //pack this
				odd; //odd is unique List
			}
			case Nil {
				new Nil; // keep the single element and return a new Nil list
			}
		}
	}
	
}

state Nil case of List {
	method void add(immutable Integer >> void i)[unique Nil >> unique Cons] {
		this <- Cons {
			var unique List next = new Nil;
			val immutable Integer data = i;
		}
	}
	
	method unique List splitList()[unique Nil >> unique Nil] {
		new Nil;
	}
}

val (unique List >> none List)
		[(unique List >> none List, unique List >> none List) -> unique List merge] 
	-> unique List mergeSort = 
	fn (unique List >> none List list)
		[(unique List >> none List, unique List >> none List) -> unique List merge]
	=> {
		match (list) {
			case Nil {
				list
			}
			case Cons {
				match (list!next) {
					case Nil {
						list
					}
					case Cons {
						val unique List half = list.splitList();
						val unique List sorted1 = mergeSort(list);
						val unique List sorted2 = mergeSort(half);
						merge(sorted1, sorted2);
					}
				}
			}
		}
		
	};

val (unique List >> none List, unique List >> none List) -> unique List merge = 
	fn (unique List >> none List l, unique List >> none List r) => {
		match (l) {
			case Cons {
				match (r) {
					case Cons {
						match (l!data <= r!data) {
							case True {
								val unique List rest = l!next; //unpack l
								val unique List toAppend = merge(rest,r);
								l.next = toAppend; //pack l
								l
							}
							case False {
								val unique List rest = r!next; //unpack r
								val unique List toAppend = merge(l,rest);
								r.next = toAppend; //pack r
								r
							}
						}
					}
					case Nil {
						l
					}
				}
			}
			case Nil {
				r
			}
		}
	};
	
val (immutable List >> immutable List) -> void printList = 
	fn (immutable List >> immutable List list) => { 
		match(list) {
			case Nil { }
			case Cons { 
				//java.lang.System.out.print("" + list!data + " ");
				printList(list.next);
			}
		}
	};	
	
method void main() [void >> void] {
	val unique List testList = 
		new Cons {
			val immutable Integer data = 4;
			var unique List next = new Cons {
				val immutable Integer data = 8;
				var unique List next = new Cons {
					val immutable Integer data = 1;
					var unique List next = new Cons {
						val immutable Integer data = 2;
						var unique List next = new Cons {
							val immutable Integer data = 5;
							var unique List next = new Cons {
								val immutable Integer data = 0;
								var unique List next = new Nil;
							};
						};	
					};
				};
			};
		};
	//printList(testList);
	val unique List sortedList = mergeSort(testList);
	//java.lang.System.out.println("");
	printList(sortedList);
}